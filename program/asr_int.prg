*-------------------------------------------------------
* Project.........: Basis.pjx
* File............: Startup.prg
* Module/Procedure: Startup()
* Called by.......: <NA>
* Parameters......: <tcCommand>
* Returns.........: <none>
* Notes...........: Обмен данными с сервером AS@R
					*!*	"WRC" - Загрузка на сервер справочника товаров
					*!*	"WRS" - Загрузка на сервер справочника дисконтных карт
					*!* "WPR" - Загрузка на сервер справочника персонала
					*!*	"RDC" - Чтение с сервера данных о продажах
					*!* "VER" - Версия программы
*------------------------------------------------------------------------------
LPARAMETERS	tcCommand

*10.08.2005 11:04 ->Определим версию	
#DEFINE LOC_DRV_NAME [ASR_INT.APP]
#DEFINE LOC_VERSION  [1.0.0.0]
*------------------------------------------------------------------------------

*05.08.2005 11:28 ->Объявление и инициализация переменных
LOCAL	llLibLoad, ;
		loRes, ;
		lcPath, ;
		loAsrInt
*------------------------------------------------------------------------------

*******************************************************************************
* Прочитаем путь для записи флагов
*******************************************************************************

*10.08.2005 11:09 ->Создаем объект для работы с INI файлами
IF !([GENERAL_V1.VCX]$SET([CLASSLIB]))
	SET CLASSLIB TO GENERAL_V1.VCX ALIAS _GENERAL_V1 ADDITIVE
	llLibLoad = .T.
ELSE
	llLibLoad = .F.	
ENDIF
loRes = CREATEOBJECT([RESOURCE],FULLPATH([ASR_INT.INI]))
IF llLibLoad
	RELEASE CLASSLIB ALIAS _GENERAL_V1
ENDIF
*------------------------------------------------------------------------------

*10.08.2005 11:09 ->Прочитаем местоположение каталога со флагами
lcPath = ALLTRIM(loRes.GetParam([EXCHANGE],[UPLOAD_FLG_DIR]))
lcPath = IIF(RIGHT(lcPath,1)=[\],lcPath,lcPath+[\])
*------------------------------------------------------------------------------

*******************************************************************************
* Проверяем корректность пути и отсутствие файлов загрузки сервера
*******************************************************************************
IF TYPE([lcPath])#[C] OR !DIRECTORY(lcPath)
	MESSAGEBOX(LOC_DRV_NAME+[(]+LOC_VERSION+[): ]+[Путь для загрузки сервера неверен!],48,[Предупреждение...])
	RETURN .F.
ENDIF
*------------------------------------------------------------------------------

*10.08.2005 11:04 ->При запуске без параметров - выведем информацию о версии
IF TYPE([tcCommand])#[C]
	tcCommand = []
ELSE
	tcCommand = UPPER(ALLTRIM(tcCommand))
ENDIF
*------------------------------------------------------------------------------

*10.08.2005 11:04 ->Запустим форму для работы с драйвером
DO CASE
	CASE INLIST(tcCommand,[WRC],[WRS],[WPR],[RDC])

		*02.05.2004 15:47 ->Создадим объект для работы с драйвером
		IF !([ASR_INT_V1.VCX]$SET([CLASSLIB]))
			SET CLASSLIB TO ASR_INT_V1.VCX ALIAS ASR_INT_V1 ADDITIVE
			llLibLoad = .T.
		ELSE
			llLibLoad = .F.	
		ENDIF
		loAsrInt = CREATEOBJECT([frmLoadTvr],tcCommand,lcPath)
		IF llLibLoad
			RELEASE CLASSLIB ALIAS ASR_INT_V1
		ENDIF
		*------------------------------------------------------------------------------
	
		loAsrInt.Show(1)
	
	CASE tcCommand=[VER] &&информацию о версии
	
		*10.08.2005 11:04 ->Сообщим номер версии
		RETURN LOC_VERSION
		*------------------------------------------------------------------------------
	
	OTHERWISE
	
		*10.08.2005 11:04 ->Ругаемся
		MESSAGEBOX(LOC_DRV_NAME+[(]+LOC_VERSION+[): Неверная команда или данная команда не поддерживается данной версией драйвера!],48,[Предупреждение...])
		*------------------------------------------------------------------------------
		
ENDCASE
*------------------------------------------------------------------------------

*******************************************************************************
******************************** END PROCEDURE ********************************
*******************************************************************************
